plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'name.remal.sonarlint' version '1.5.0'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'io.gitlab.arturbosch.detekt' version '1.19.0'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.1'
}

group 'com.nelkinda.templates' // TODO change
version '0.0.1-SNAPSHOT'

sourceCompatibility = 17

repositories {
    mavenCentral()
}

dependencyManagement {
    dependencies {
        dependencySet(group: 'io.cucumber', version: '7.2.3') {
            entry 'cucumber-java'
            entry 'cucumber-junit-platform-engine'
        }
        dependencySet(group: 'org.junit.jupiter', version: '5.8.2') {
            entry 'junit-jupiter'
            entry 'junit-jupiter-api'
            entry 'junit-jupiter-engine'
            entry 'junit-jupiter-params'
        }
        dependencySet(group: 'org.junit.platform', version: '1.8.2') {
            entry 'junit-platform-commons'
            entry 'junit-platform-engine'
            entry 'junit-platform-launcher'
            entry 'junit-platform-suite-api'
            entry 'junit-platform-suite-commons'
            entry 'junit-platform-suite-engine'
        }
        dependency 'org.mockito:mockito-core:4.3.1'
        dependency 'org.pitest:pitest-junit5-plugin:0.15'
        dependency 'org.pitest:pitest-command-line:1.9.0'
    }
}
dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation 'io.cucumber:cucumber-java'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.junit.platform:junit-platform-commons'
    testImplementation 'org.junit.platform:junit-platform-engine'
    testImplementation 'org.junit.platform:junit-platform-suite-api'
    testImplementation 'org.junit.platform:junit-platform-suite-commons'
    testImplementation 'org.junit.platform:junit-platform-suite-engine'
    testImplementation 'org.mockito:mockito-core'

    pitest 'org.pitest:pitest-junit5-plugin'
    pitest 'org.pitest:pitest-command-line'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.enabled true
    }
}
test.finalizedBy jacocoTestReport

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable"]
        jvmTarget = "17"
    }
}

ktlint {
    version = '0.40.0'
    verbose = true
}

pitest {
    targetClasses.set(['com.nelkinda.template.*']) // TODO change
    pitestVersion.set('1.7.3')
    timestampedReports.set(false)
    testPlugin.set('junit5')
    outputFormats.set(['XML', 'HTML'])
    mutators.set(['DEFAULTS', 'STRONGER', 'CONSTRUCTOR_CALLS', 'INLINE_CONSTS', 'REMOVE_CONDITIONALS', 'REMOVE_INCREMENTS'])
    mutationThreshold.set(100)
    coverageThreshold.set(100)
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion '7.4'
    distributionType = Wrapper.DistributionType.ALL
}
